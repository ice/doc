<html>


    <head>
        <link rel="stylesheet" type="text/css" href="../../asset/static/prettify.css" />
        <link rel="stylesheet" type="text/css" href="../../asset/static/style.css" />
        
        
        <script src="../../asset/static/jquery.min.js"></script>
        <script src="../../asset/static/prettify.js"></script>
        <script src="../../asset/api_definition.js"></script>

        <script>var ZepCurrentPath = '../../'</script>

        <script src="../../asset/static/script.js"></script>

                
    </head>


    <body onload="prettyPrint()">
        <div id="top-bar">
            <div id="top-left">
                Zephir Documentation <span class="version">v1.0.1</span>
            </div>
            <div id="top-right">

            </div>
        </div>

        <div id="body">

            <div id="body-left">

                <div class="header-search">

                    <input class="search-box" placeholder="Search for a class or namespace " type="text" />
             
                    <div id="menu-wrapper">


                    </div>

                </div>

            </div>
            <div id="body-right">
                <div class="content">
                    
<div class="breadcrumb">
    
    <ul>
        
        <li><a href="../../index.html">Home</a></li>
        
                <li><a href="../../classes.html">Classes</a></li>
                <li><a href="../../class/Ice/Loader.html">Ice\Loader</a></li>
                <li><a >Source</a></li>
                
    </ul>
    
</div>
<div class="class-header source clearfix">
    
    <div class="access-buttons">
        <a class="zep-button" href="../../class/Ice/Loader.html">Class</a>

                <a class="zep-button" href="https://github.com/ice/framework/tree/master/ice/loader.zep">Github</a>
            </div>
    
    <div class="class-full-name">
        
        <span class="class-type">Class</span>
        
                <span class="namespace-piece">
            <a href="../../namespace/Ice.html">Ice</a>
        </span>
                <h1 class="class-name">Loader</h1>
    </div>
    
</div>


<pre class="zephir-source-file prettyprint linenums">
namespace Ice;

/**
 * PSR-4 autoloader class.
 *
 * @package     Ice/Loader
 * @category    Library
 * @author      Ice Team
 * @copyright   (c) 2014-2015 Ice Team
 * @license     http://iceframework.org/license
 */
class Loader
{

    protected _prefixes = [];

    /**
     * Register loader with SPL autoloader stack.
     * 
     * @return void
     */
    public function register()
    {
        spl_autoload_register([this, "loadClass"]);
    }

    /**
     * Adds a base directory for a namespace prefix.
     *
     * @param string prefix The namespace prefix
     * @param string baseDir A base directory for class files in the namespace
     * @param bool prepend If true, prepend the base directory to the stack instead of appending it; this causes it to
     * be searched first rather than last
     * @return Loader
     */
    public function addNamespace(string prefix, string baseDir, boolean prepend = false)
    {
        // normalize namespace prefix
        let prefix = trim(prefix, "\\") . "\\";

        // normalize the base directory with a trailing separator
        let baseDir = rtrim(baseDir, "/") . DIRECTORY_SEPARATOR,
            baseDir = rtrim(baseDir, DIRECTORY_SEPARATOR) . "/";

        // initialize the namespace prefix array
        if !isset this->_prefixes[prefix] {
            let this->_prefixes[prefix] = [];
        }

        // retain the base directory for the namespace prefix
        if prepend {
            array_unshift(this->_prefixes[prefix], utf8_encode(baseDir));
        } else {
            array_push(this->_prefixes[prefix], utf8_encode(baseDir));
        }
        return this;
    }

    /**
     * Loads the class file for a given class name.
     *
     * @param string className The fully-qualified class name
     * @return mixed The mapped file name on success, or boolean false on failure
     */
    public function loadClass(string className)
    {
        var prefix, pos, relativeClass, mappedFile;

        // the current namespace prefix
        let prefix = className;

        // work backwards through the namespace names of the fully-qualified
        // class name to find a mapped file name
        let pos = strrpos(prefix, "\\");
        while pos !== false {
            // retain the trailing namespace separator in the prefix
            let prefix = substr(className, 0, pos + 1);

            // the rest is the relative class name
            let relativeClass = substr(className, pos + 1);

            // try to load a mapped file for the prefix and relative class
            let mappedFile = this->loadMappedFile(prefix, relativeClass);
            if mappedFile {
                return mappedFile;
            }

            // remove the trailing namespace separator for the next iteration
            // of strrpos()
            let prefix = rtrim(prefix, "\\");
            let pos = strrpos(prefix, "\\");
        }

        // never found a mapped file
        return false;
    }

    /**
     * Load the mapped file for a namespace prefix and relative class.
     * 
     * @param string $prefix The namespace prefix
     * @param string $relative_class The relative class name
     * @return mixed Boolean false if no mapped file can be loaded, or the name of the mapped file that was loaded
     */
    protected function loadMappedFile(string prefix, string relativeClass)
    {
        var baseDir, file;

        // are there any base directories for this namespace prefix?
        if !isset this->_prefixes[prefix] {
            return false;
        }

        // look through base directories for this namespace prefix
        for baseDir in this->_prefixes[prefix] {
            // replace the namespace prefix with the base directory,
            // replace namespace separators with directory separators
            // in the relative class name, append with .php
            let file = baseDir . str_replace("\\", DIRECTORY_SEPARATOR, relativeClass) . ".php",
                file = baseDir . str_replace("\\", "/", relativeClass) . ".php";

            // if the mapped file exists, require it
            if this->requireFile(file) {
                // yes, we"re done
                return file;
            }
        }

        // never found it
        return false;
    }

    /**
     * If a file exists, require it from the file system.
     * 
     * @param string $file The file to require
     * @return bool True if the file exists, false if not
     */
    protected function requireFile(string file)
    {
        if file_exists(file) {
            require file;
            return true;
        }
        return false;
    }
}
</pre>                </div>
            </div>

        </div>

    </body>



</html>