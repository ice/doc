<!DOCTYPE html>
<html lang="en">
    <head>
        <link rel="stylesheet" type="text/css" href="../../asset/static/css/bootstrap.min.css" />
        <link rel="stylesheet" type="text/css" href="../../asset/static/css/tomorrow.min.css" id="tomorrow" />
        <link rel="stylesheet" type="text/css" href="../../asset/static/css/tomorrow-night.min.css" id="tomorrow-night" disabled />
        <link rel="stylesheet" type="text/css" href="../../asset/static/css/style.css" />
    </head>
    <body>
        <header class="navbar navbar-default navbar-fixed-top">
            <div id="top-bar" class="container-fluid">
                <div id="top-left" class="navbar-header">
                    <a href="../../index.html" class="navbar-brand">
                                                    ice Documentation <span class="version">v1.1.1</span>
                                            </a>
                </div>
                <div id="top-right" class="collapse navbar-collapse">

                </div>
            </div>
        </header>
        <div id="body" class="container-fluid">
            <div class="row">
                <div id="body-left" class="col-lg-3 col-sm-4">
                    <div class="header-search">
                        <input class="search-box form-control" placeholder="Search for a class or namespace " type="text" />
                        <div id="menu-wrapper">
                        </div>
                    </div>
                </div>
                <div id="body-right" class="col-lg-9 col-sm-8">
                    <div class="content">
                        
<div>
    
    <ul class="breadcrumb">
        
        <li><a href="../../index.html">Home</a></li>
        
                <li><a href="../../classes.html">Classes</a></li>
                <li><a href="../../class/Ice/Di.html">Ice\Di</a></li>
                <li><a >Source</a></li>
                
    </ul>
    
</div>
<div class="class-header source clearfix">
    
    <div class="access-buttons pull-right">
        <a class="zep-button btn btn-default" href="../../class/Ice/Di.html">Class</a>

                <a class="zep-button btn btn-default" href="https://github.com/ice/framework/tree/dev/ice/di.zep">Github</a>
            </div>
    
    <div class="class-full-name">
        
        <span class="class-type">Class</span>
        
                <span class="namespace-piece">
            <a href="../../namespace/Ice.html">Ice</a>
        </span>
                <h1 class="class-name">Di</h1>
    </div>
    
</div>


<pre class="zephir-source-file"><code>
namespace Ice;

use ReflectionClass;
use ReflectionParameter;

/**
 * Component that implements Dependency Injection and a container for the services.
 *
 * @package     Ice/Di
 * @category    Component
 * @author      Ice Team
 * @copyright   (c) 2014-2015 Ice Team
 * @license     http://iceframework.org/license
 */
class Di extends Arr
{

    protected static di;
    protected hooks = [];
    protected defaults = [] { set };

    /**
     * Di constructor. Sets root path.
     *
     * @param array data Array of services
     */
    public function __construct(array data = [])
    {
        parent::__construct(data);

        let self::di = this;
    }

    /**
     * Return the last DI created.
     *
     * @return Di
     */
    public static function $fetch() -&gt; &lt;Di&gt;
    {
        return self::di;
    }

    /**
     * Resolves the service based on its configuration.
     *
     * @param string key Service name
     * @param mixed parameters Definition
     */
    public function get(string key, var parameters = null)
    {
        var predefined;

        if this-&gt;has(key) {
            return parent::get(key, parameters);
        } else {
            fetch predefined, this-&gt;getDefaults()[key];

            return this-&gt;set(key, predefined ? predefined : parameters);
        }
    }

    /**
     * Registers a service in the services container.
     *
     * @param string key Service name
     * @param mixed value Definition
     */
    public function set(string key, var value)
    {
        var service;

        let service = this-&gt;resolve(value),
            this-&gt;data[key] = service;

        return service;
    }

    /**
     * Get default services.
     *
     * @return array
     */
    public function getDefaults()
    {
        return array_merge([
            &quot;assets&quot;: &quot;Ice\\Assets&quot;,
            &quot;cookies&quot;: &quot;Ice\\Cookies&quot;,
            &quot;crypt&quot;: &quot;Ice\\Crypt&quot;,
            &quot;dispatcher&quot;: &quot;Ice\\Mvc\\Dispatcher&quot;,
            &quot;dump&quot;: &quot;Ice\\Dump&quot;,
            &quot;filter&quot;: &quot;Ice\\Filter&quot;,
            &quot;flash&quot;: &quot;Ice\\Flash&quot;,
            &quot;loader&quot;: &quot;Ice\\Loader&quot;,
            &quot;request&quot;: &quot;Ice\\Http\\Request&quot;,
            &quot;response&quot;: &quot;Ice\\Http\\Response&quot;,
            &quot;router&quot;: &quot;Ice\\Mvc\\Router&quot;,
            &quot;session&quot;: &quot;Ice\\Session&quot;,
            &quot;tag&quot;: &quot;Ice\\Tag&quot;,
            &quot;text&quot;: &quot;Ice\\Text&quot;,
            &quot;url&quot;: &quot;Ice\\Url&quot;,
            &quot;view&quot;: &quot;Ice\\Mvc\\View&quot;
        ], this-&gt;defaults);
    }

    /**
     * Resolve service.
     *
     * @param mixed service Definition
     * @return object Service
     */
    public function resolve(var service)
    {
        var params;

        if typeof service == &quot;null&quot; {
            return null;
        } elseif typeof service == &quot;string&quot; {
            if class_exists(service) {
                let service = this-&gt;build(service);
            }
        } else {
            if typeof service == &quot;object&quot; {
                if service instanceof \Closure {
                    let service = call_user_func(service);
                }
            } else {
                // Array definitions store class name at first parameter
                if typeof service == &quot;array&quot; {
                    let params = current(service),
                        service = this-&gt;build(key(service), params == &quot;array&quot; ? params : [params]);
                }
            }
        }

        if typeof service != &quot;object&quot; {
            throw new Exception(sprintf(&quot;Service &apos;%s&apos; cannot be resolved&quot;, service));
        }

        return service;
    }

    /**
     * Build an instance of the given class.
     * 
     * @param string service
     * @return mixed
     */
    public function build(var service, var parameters = [])
    {
        var reflector, constructor, dependencies;

        if typeof service == &quot;object&quot; {
            if service instanceof \Closure {
                return call_user_func_array(service, parameters);
            }
        }

        let reflector = new ReflectionClass(service);

        if !reflector-&gt;isInstantiable() {
            throw new Exception([&quot;Service &apos;%s&apos; is not instantiable&quot;, service]);
        }
        
        let constructor = reflector-&gt;getConstructor();
        
        if is_null(constructor) {
            return new {service}();
        }

        let dependencies = constructor-&gt;getParameters(),
            parameters = this-&gt;getParameters(dependencies, parameters),
            dependencies = this-&gt;getDependencies(dependencies, parameters);

        return reflector-&gt;newInstanceArgs(dependencies);
    }
    
    /**
     * If extra parameters are passed by numeric ID, rekey them by argument name.
     *
     * @param array dependencies
     * @param array parameters
     * @return array
     */
    protected function getParameters(array dependencies, array parameters)
    {
        var key, value, dependency;

        for key, value in parameters {
            if is_numeric(key) {
                unset parameters[key];

                let dependency = dependencies[key],
                    parameters[dependency-&gt;name] = value;
            }
        }

        return parameters;
    }

    /**
     * Build up a list of dependencies for a given methods parameters.
     *
     * @param array parameters
     * @return array
     */
    public function getDependencies(array parameters, array primitives = [])
    {
        var dependencies, parameter, dependency;

        let dependencies = [];
        
        for parameter in parameters {
            let dependency = parameter-&gt;getClass();
            
            if array_key_exists(parameter-&gt;name, primitives) {
                let dependencies[] = primitives[parameter-&gt;name];
            } elseif is_null(dependency) {
                if !parameter-&gt;isOptional() {
                    let dependencies[] = this-&gt;resolveNonClass(parameter);
                }
            } else {
                let dependencies[] = this-&gt;build(dependency-&gt;name);
            }
        }
        
        return dependencies;
    }
    
    /**
     * Determine what to do with a non-class value.
     *
     * @param ReflectionParameter parameter
     * @return mixed
     *
     * @throws Exception
     */
    public function resolveNonClass(&lt;ReflectionParameter&gt; parameter)
    {
        if parameter-&gt;isDefaultValueAvailable() {
            return parameter-&gt;getDefaultValue();
        }
        
        throw new Exception([&quot;Unresolvable dependency resolving &apos;%s&apos; in class &apos;%s&apos;&quot;, parameter, parameter-&gt;getDeclaringClass()-&gt;getName()]);
    }

    /**
     * Set handlers for error, exeception and shutdown.
     * Class should contain method handler, errorHandler and shutdownHandler.
     *
     * @param string className
     * @return object Di
     */
    public function errors(string className = &quot;Ice\\Exception&quot;)
    {
        // Enable Ice exception handling, adds stack traces and error source.
        set_exception_handler([className, &quot;handler&quot;]);

        // Enable Ice error handling, converts all PHP errors to exceptions.
        set_error_handler([className, &quot;errorHandler&quot;]);

        // Enable Ice shutdown handler, which catches E_FATAL errors.
        register_shutdown_function([className, &quot;shutdownHandler&quot;]);

        return this;
    }

    /**
     * Assign hook.
     *
     * @param string name The hook name
     * @param mixed callable A callable object
     * @param int priority The hook priority; 0 = high, 10 = low
     */
    public function hook(string name, var callback, int priority = 10)
    {
        if !isset this-&gt;hooks[name] {
            let this-&gt;hooks[name] = [[]];
        }

        if typeof callback == &quot;callable&quot; {
            let this-&gt;hooks[name][priority][] = callback;
        }
    }

    /**
     * Invoke hook.
     *
     * @param string name The hook name
     * @param mixed hookArg Argument for hooked functions
     */
    public function applyHook(string name, array args = null)
    {
        var priority, callback;

        if !isset this-&gt;hooks[name] {
            let this-&gt;hooks[name] = [[]];
        }

        if !empty this-&gt;hooks[name] {
            // Sort by priority, low to high, if there&apos;s more than one priority
            //if count(this-&gt;hooks[name]) &gt; 1 {
            //    ksort(this-&gt;hooks[name]);
            //}

            for priority in this-&gt;hooks[name] {
                if !empty priority {
                    for callback in priority {
                        call_user_func_array(callback, args);
                    }
                }
            }
        }
    }

    /**
     * Get hook listeners.
     * Return an array of registered hooks. If `$name` is a valid hook name, only the listeners attached to that hook
     * are returned. Else, all listeners are returned as an associative array whose keys are hook names and whose values
     * are arrays of listeners.
     *
     * @param string name A hook name (Optional)
     * @return array|null
     */
    public function getHooks(string name = null) -&gt; array | null
    {
        if name {
            return isset this-&gt;hooks[name] ? this-&gt;hooks[name] : null;
        } else {
            return this-&gt;hooks;
        }
    }

    /**
     * Clear hook listeners.
     * Clear all listeners for all hooks. If `$name` is a valid hook name, only the listeners attached to that hook
     * will be cleared.
     *
     * @param  string $name A hook name (Optional)
     */
    public function clearHooks(string name = null)
    {
        var key;

        if name &amp;&amp; isset this-&gt;hooks[name] {
            let this-&gt;hooks[name] = [[]];
        } else {
            for key in array_keys(this-&gt;hooks) {
                let this-&gt;hooks[key] = [[]];
            }
        }
    }

    /**
     * Magic method to get or set services using setters/getters.
     *
     * @param string method
     * @param mixed arguments
     * @return mixed
     */
    public function __call(string! method, arguments = null)
    {
        var value;

        // If the magic method starts with &quot;get&quot; we try to get a service with that name
        if starts_with(method, &quot;get&quot;) {
            fetch value, arguments[0];
            return this-&gt;get(lcfirst(substr(method, 3)), value);
        }

        // If the magic method starts with &quot;set&quot; we try to set a service using that name
        if starts_with(method, &quot;set&quot;) {
            fetch value, arguments[0];
            return this-&gt;set(lcfirst(substr(method, 3)), value);
        }

        // The method doesn&apos;t start with set/get throw an exception
        throw new Exception(&quot;Call to undefined method or service &apos;&quot; . method . &quot;&apos;&quot;);
    }
}
</code></pre>                    </div>
                </div>
            </div>
        </div>

        <script src="../../asset/static/js/jquery.min.js"></script>
        <script src="../../asset/static/js/bootstrap.min.js"></script>
        <script src="../../asset/static/js/highlight.pack.js"></script>
        <script src="../../asset/static/js/jquery.slimscroll.min.js"></script>
        <script src="../../asset/api_definition.js"></script>

        <script>var ZepCurrentPath = '../../'</script>
        <script src="../../asset/static/js/script.js"></script>
        <script type="text/javascript">
            $(window).on("load resize", function() {
                $('#menu-wrapper').slimscroll({ height: $(window).height() - 100, size: '3px' });
            });
        </script>
        <script type="text/javascript">
            $(document).ready(function() { $('pre code').each(function(i, block) { hljs.highlightBlock(block); }); });
            if ($('.zephir-source-file').length) {
                $('link[id]').each(function(i, link) {
                    link.disabled = (link.id != 'tomorrow-night');
                });
            }
        </script>
            </body>
</html>